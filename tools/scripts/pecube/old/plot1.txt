# to plot this file use:
# for i in xyz_*.dat; do gnuplot -e "inFile='$i'; outFile='${i/dat/png}'" ../../plot1.txt; done
#
# to generate xyz data from tecplot files:
# for i in topo_tec_*.dat; do tail -n +5 $i | head -n62150 > ${i/topo_tec_/xyz_}; done
#

# save plot to png file, size: 1600 x 1200
set terminal png size 1600,2400

# set filename for png image file
set output outFile

# view from top, x and y coordinates
set view map

# set labels for axis
set xlabel "x [km]"
set ylabel "y [km]"

# set style for data file
set style data points

# set color model for color bar
set palette rgbformulae 33,13,10

# set new style for lines at index 100
set style line 100 pointtype 7 palette

# set the size for all the data points
set pointsize 0.9

# show grid for x and y axis, line style -1 means black color
set grid linestyle -1

set xrange [0:400]
set yrange [0:250]

# make two plots, two lines, one column
set multiplot layout 5,1 title "topography for '".inFile."'"

# set label for color boxes individually

set cblabel "z [km]"
set cbrange [-0.4:1.8]
splot inFile using 1:2:3 linestyle 100 notitle

set cblabel "ice thickness [m]"
set cbrange [-200:1400]
splot inFile using 1:2:13 linestyle 100 notitle

set cblabel "mass balance [1/y]"
set cbrange [-22:2]
splot inFile using 1:2:14 linestyle 100 notitle

set cblabel "total topography [km]"
set cbrange [-200:1800]
splot inFile using 1:2:15 linestyle 100 notitle

set cblabel "precipitation [m/y]"
set cbrange [0:3.5]
splot inFile using 1:2:5 linestyle 100 notitle

